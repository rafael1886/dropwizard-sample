plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'application'
    id 'com.google.cloud.tools.jib' version '2.6.0'
}

group 'org.pl.dropwizard'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

sourceSets.main.output.resourcesDir = sourceSets.main.java.outputDir
sourceSets.test.output.resourcesDir = sourceSets.test.java.outputDir

wrapper {
    gradleVersion = '6.8.2'
}
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-client:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-jdbi3:${dropwizardVersion}"

    implementation "org.flywaydb:flyway-core:${flywaydbVersion}"
    implementation "org.postgresql:postgresql:${postgresVersion}"

    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
// https://mvnrepository.com/artifact/io.dropwizard/dropwizard-views-freemarker
    implementation "io.dropwizard:dropwizard-views-freemarker:${freemarkerVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.testcontainers:postgresql:${testContainersVersion}"

}

mainClassName = 'org.pl.dropwizard.StartApplication'

jib.to.image = 'gcr.io/dropwizard-sample'

run {
    args = ['server', 'config.yml']
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'dropwizard-sample.jar'
}